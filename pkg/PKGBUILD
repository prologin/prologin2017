# Maintainer: Prologin <serveur@prologin.org>

_url=git@github.com/prologin
pkgname=prologin2017
pkgrel=1
pkgver=r167
pkgdesc="Stechec2 game: ${pkgver}"
arch=('x86_64')
url="http://prologin.org/"
license=('GPL')
depends=('stechec2' 'gflags' 'zeromq' 'ocaml' 'python-yaml' 'ruby')
makedepends=('git')
source=("git+ssh://${_url}/stechec2"
        "git+ssh://${_url}/${pkgname}")
sha512sums=(SKIP
            SKIP)

pkgver() {
    cd "$pkgname"
    echo r$(git rev-list --count HEAD)
}

prepare() {
    cd stechec2
    ln --force --symbolic "$srcdir/$pkgname" games/
}

build() {
    cd stechec2
    ./waf.py configure build --prefix=/usr --libdir=/usr/lib \
                             --with-games="$pkgname" --games-only
}

package() {
    cd stechec2
    ./waf.py install --destdir="$pkgdir"

    yaml_file="$srcdir/$pkgname/$pkgname.yml"
    stechec2-generator player "$yaml_file" \
	"$pkgdir/usr/share/stechec2/$pkgname/player"
    stechec2-generator server "$yaml_file" \
	"$pkgdir/usr/share/stechec2/$pkgname/makefiles"
}
